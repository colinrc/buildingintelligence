<?xml version="1.0" encoding="utf-8"?>
<mx:ComboBox xmlns:mx="http://www.adobe.com/2006/mxml">
	<mx:Script>
<![CDATA[

   private var candidateValue:Object;
   private var valueDirty:Boolean = false;
   private var candidateDataProvider:Object;
   private var dataProviderDirty:Boolean = false;
   
   private function applyValue(val:Object):void {
       if ((val != null) && (dataProvider != null)) {
               
          for (var i : int = 0; i < dataProvider.length; i++) {
               if ( val == dataProvider[i].data || val == dataProvider[i].label) {
                  ;  selectedIndex = i;
                  ;  return;
       }    }    }
       selectedIndex = -1;
   }      
   public function set value(val:Object) : void {
       candidateValue = val;
       valueDirty = true;
       invalidateProperties();
   }
   override public function set dataProvider(value:Object):void {
       candidateDataProvider = value;
       dataProviderDirty = true;
       invalidateProperties();
   }

   override protected function commitProperties():void {
       super.commitProperties();

       if (dataProviderDirty) {
          super.dataProvider = candidateDataProvider;
          dataProviderDirty = false;
       }

       if (valueDirty) {
          applyValue(candidateValue);
          valueDirty = false;
       }
   }
   	override public function set data(data:Object):void {
		super.data = data;
		if (listData && listData is DataGridListData) {
			candidateValue = data[DataGridListData(listData).dataField];
			valueDirty = true;
		}
		else if (listData is ListData && ListData(listData).labelField in data) {
			candidateValue = data[ListData(listData).labelField];
			valueDirty = true;
		}
	}


	override public function get value():Object {
		if (editable){
			return text;
		}
		var item:Object = selectedItem;
		
		if (item == null || typeof(item) != "object") {
			return item;
		}
		return "data" in item ? item.data : item.label;
	} 
   
   ]]>
</mx:Script>

</mx:ComboBox>
