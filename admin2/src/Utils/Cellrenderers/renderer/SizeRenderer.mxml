<?xml version="1.0" encoding="utf-8"?>
<mx:VBox xmlns:mx="http://www.adobe.com/2006/mxml" horizontalAlign="center">
   <mx:Script>
        <![CDATA[
            // Define a property for returning the new value to the cell.
            public var result:String="";
            
            [Bindable]
            private var dpValue:Array = [ 
            	{ label:"S  (small)", data:"small" },
            	{ label:"M  (medium)", data:"medium"},
            	{ label:"L  (large)", data:"large"},
            	{ label:"XL (extra-large)", data:"extra-large"} ];
            
        
			// Override the set method for the data property.
			override public function set data(value:Object):void 
			{
				super.data = value;
				if (value != null) 
				{
					var currentValue:String = value.size;
					var len:int = dpValue.length;
					for (var i:int = 0; i < len; i++) 
					{
						if (dpValue[i].data == currentValue) 
						{
							editor.selectedIndex = i;
							break;
						}
					}
				}
			} 
			
			public function onChange():void 
			{
				var index:int = editor.selectedIndex;
				result = dpValue[index].data;
			}          
        ]]>
    </mx:Script>
 	<mx:ComboBox id="editor" dataProvider="{dpValue}" width="130" change="onChange()"/>
</mx:VBox>
